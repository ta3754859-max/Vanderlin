/datum/action/cooldown/spell/cure_rot
	name = "Cure Rot"
	desc = "Cleanse a body of rot, deadites will perish."
	button_icon_state = "cure_rot"
	sound = 'sound/magic/revive.ogg'
	charge_sound = 'sound/magic/holycharging.ogg'

	cast_range = 1
	spell_type = SPELL_DIVINE_MIRACLE
	antimagic_flags = MAGIC_RESISTANCE_HOLY
	associated_skill = /datum/attribute/skill/magic/holy
	required_items = list(/obj/item/clothing/neck/psycross/silver)

	charge_time = 2 SECONDS
	charge_slowdown = 0.8
	cooldown_time = 2 MINUTES
	spell_cost = 50
	var/need_cross = TRUE
	var/breaks_blood_curse = TRUE

/datum/action/cooldown/spell/cure_rot/is_valid_target(atom/cast_on)
	. = ..()
	if(!.)
		return FALSE
	return ishuman(cast_on)

/datum/action/cooldown/spell/cure_rot/before_cast(mob/living/carbon/human/cast_on)
	. = ..()
	if(. & SPELL_CANCEL_CAST)
		return

	if(need_cross)
		for(var/obj/structure/fluff/psycross/S in view(5, owner))
			if(S)
				break
			to_chat(owner, span_warning("I need a holy cross."))
			reset_spell_cooldown()
			return . | SPELL_CANCEL_CAST

	var/obj/item/bodypart/chest = cast_on.get_bodypart(BODY_ZONE_CHEST)
	var/obj/item/bodypart/head = cast_on.get_bodypart(BODY_ZONE_HEAD)
	if(chest.skeletonized || head.skeletonized)
		to_chat(owner, span_warning("They are too far gone."))
		reset_spell_cooldown()
		return . | SPELL_CANCEL_CAST

/datum/action/cooldown/spell/cure_rot/cast(mob/living/carbon/human/cast_on)
	. = ..()
	var/was_zombie = IS_DEADITE(cast_on)
	var/has_rot = FALSE
	if(!was_zombie)
		for(var/obj/item/bodypart/bodypart as anything in cast_on.bodyparts)
			if(HAS_TRAIT(bodypart, TRAIT_ROTTEN))
				has_rot = TRUE
				break
			if(bodypart.germ_level >= INFECTION_LEVEL_ONE*0.2)
				has_rot = TRUE
				break
		for(var/obj/item/organ/organs as anything in cast_on.internal_organs)
			if(organs.germ_level >= INFECTION_LEVEL_ONE*0.2)
				has_rot = TRUE
				break

	if(breaks_blood_curse)
		if(cast_on.has_status_effect(/datum/status_effect/debuff/revive_bloodmagic) || cast_on.has_status_effect(/datum/status_effect/debuff/blood_mark))
			if(!prob(33))
				cast_on.visible_message(
					span_warning("Divine Light struggles to burn through the Blood Curse upon [cast_on]!"),
					span_bloody("The Blood Curse is resisting the Divine!"),
				)
				return FALSE
			cast_on.remove_status_effect(/datum/status_effect/debuff/revive_bloodmagic)
			cast_on.remove_status_effect(/datum/status_effect/debuff/blood_mark)
			cast_on.visible_message(
				span_warning("Divine Light burns through the Blood Curse upon [cast_on]!"),
				span_bloody("The Blood Curse has been dispelled!"),
			)
			return

	if(!has_rot && !was_zombie)
		to_chat(owner, span_warning("Nothing happens."))
		return FALSE

	if(was_zombie)
		cast_on.mind.remove_antag_datum(/datum/antagonist/zombie)
		cast_on.death()

	var/datum/component/rot/rot = cast_on.GetComponent(/datum/component/rot)
	if(rot)
		rot.amount = 0

	for(var/obj/item/bodypart/rotty in cast_on.bodyparts)
		rotty.revive_limb(FALSE)
		rotty.germ_level = 0
		rotty.update_limb()
		if(rotty.can_be_disabled)
			rotty.update_disabled()

	for(var/obj/item/organ/organs as anything in cast_on.internal_organs)
		if(organs.germ_level >= INFECTION_LEVEL_ONE*0.2)
			organs.set_germ_level(INFECTION_LEVEL_ONE*0.2)

	cast_on.update_body_parts(TRUE)
	cast_on.visible_message("<span class='notice'>The rot leaves [cast_on]'s body!</span>", "<span class='green'>I feel the rot leave my body!</span>")

	if(cast_on.funeral)
		if(cast_on.ckey)
			to_chat(cast_on, span_warning("My funeral rites were undone!"))
		else
			var/mob/dead/observer/ghost = cast_on.get_ghost(TRUE, TRUE)
			if(ghost)
				to_chat(ghost, span_warning("My funeral rites were undone!"))

	cast_on.funeral = FALSE

/datum/action/cooldown/spell/cure_rot/bloodmagic
	name = "Purge Rot"
	desc = "Purge a body of rot, deadites will perish."

	associated_skill = /datum/attribute/skill/magic/blood
	spell_type = SPELL_BLOOD
	required_form = FORM_BLOOD
	required_technique = TECHNIQUE_RESTORATION
	required_level = 10
	spell_flags = SPELL_UNETCHABLE
	heretical_spell = TRUE
	antimagic_flags = MAGIC_RESISTANCE_BLOOD

	required_items = list()

	sound = 'sound/magic/marked.ogg'
	charge_sound = 'sound/magic/chargingold.ogg'

	cast_range = 2
	charge_time = 2 SECONDS
	charge_slowdown = 0.8
	cooldown_time = 2 MINUTES
	spell_cost = 200

	need_cross = FALSE
	breaks_blood_curse = FALSE
